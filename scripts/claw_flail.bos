#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 140

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>


piece  base, hips, h2, wheel, torso, ruparm, luparm, rarm, larm, rgun, lgun, rg, lg, rflare, lflare, rspike, lspike, head, l1, l2, l3, visor;

static-var  bMoving, bAiming, lastHeading;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2


recoil(anglex, anglez)
{
	anglex = <-1.000000> * (get KCOS(lastHeading)) / 100;
	anglez = <-1.000000> * (get KSIN(lastHeading)) / 100;
	
	turn base to x-axis anglex speed <150.005495>;
	turn base to z-axis anglez speed <150.005495>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to z-axis <0.000000> speed <20.000000>;
	turn base to x-axis <0.000000> speed <20.000000>;
}

walk()
{
	turn hips to x-axis <8.000000> speed <140.000000>;
	spin wheel around x-axis speed <360.000000> accelerate <100.000000>;
	sleep 100;
	return (0);
}

walklegs()
{
	turn hips to x-axis <0.000000> speed <140.000000>;
	spin wheel around x-axis speed <360.000000> accelerate <100.000000>;
	sleep 100;
	return (0);
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from torso;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MotionControl(moving, aiming, justmoved)
{
	while( TRUE )
	{
		justmoved = TRUE;
		while( TRUE )
		{
			moving = bMoving;
			aiming = bAiming;
			if( moving )
			{
				if( aiming )
				{
					call-script walklegs();
				}
				if( !aiming )
				{
					call-script walk();
				}
				justmoved = TRUE;
			}
			if( !moving )
			{
				if( justmoved )
				{
					turn hips to x-axis <0.000000> speed <100.000000>;
					stop-spin wheel around x-axis decelerate <300.000000>;
					justmoved = FALSE;
				}
				sleep 100;
			}
		}
	}
}

Create()
{
	
	bMoving = FALSE;
	bAiming = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionControl();
	start-script SmokeUnit();
}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<140>,<140>);
	turn torso to y-axis <0.000000> speed <140.016484>;
	turn luparm to x-axis <0.000000> speed <145.005495>;
	turn ruparm to x-axis <0.000000> speed <145.005495>;
	lastHeading = 0;
}


AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;
	turn torso to y-axis heading speed <300.027473>;
	turn luparm to x-axis <0.000000> - pitch speed <300.038462>;
	if (!aimReady1)
	{
		wait-for-turn torso around y-axis;
		wait-for-turn luparm around x-axis;
	}
	lastHeading = heading;
	start-script RestoreAfterDelay();
	return (1);
}


FirePrimary()
{
	turn larm to x-axis <40.000000> now;
	turn lgun to x-axis <-40.000000> now;
	emit-sfx 1024+1 from lflare;
	turn larm to x-axis <0.000000> speed <230.038462>;
	turn lgun to x-axis <0.000000> speed <230.038462>;
	call-script recoil();
	return 0;
}

QueryPrimary(piecenum)
{
	piecenum = lflare;
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
}


AimWeapon2(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;
	turn torso to y-axis heading speed <300.027473>;
	turn ruparm to x-axis <0.000000> - pitch speed <300.038462>;
	if (!aimReady1)
	{
		wait-for-turn torso around y-axis;
		wait-for-turn ruparm around x-axis;
	}
	lastHeading = heading;
	
	start-script RestoreAfterDelay();
	return (1);
}


FireWeapon2()
{
	turn rarm to x-axis <40.000000> now;
	turn rgun to x-axis <-40.000000> now;
	emit-sfx 1024+1 from rflare;
	turn rarm to x-axis <0.000000> speed <230.038462>;
	turn rgun to x-axis <0.000000> speed <230.038462>;
	call-script recoil();
	return (0);
}

QueryWeapon2(piecenum)
{
	piecenum = rflare;
}

AimFromWeapon2(piecenum)
{
	piecenum = torso;
}

SweetSpot(piecenum)
{
	piecenum = torso;
}

Killed(severity, corpsetype)
{
	if( severity <= 35 )
	{
		explode head type BITMAPONLY | BITMAP2;
	
		explode luparm type BITMAPONLY | BITMAP2;
		explode ruparm type BITMAPONLY | BITMAP4;
	
		explode visor type BITMAPONLY | BITMAP4;
		explode torso type BITMAPONLY | BITMAP5;
		return (1);
	}
	explode head type SHATTER;
	explode lgun type SHATTER;
	explode rgun type SHATTER;
	explode visor type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
	explode torso type SHATTER;
	return (2);
}
