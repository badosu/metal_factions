#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, pad, buildpt, platform1, spinner1, platform2, spinner2, platform3, spinner3, nano1, nano2, nano3, beam1, beam2, beam3, a1, a2,spike1, xl,xr, a3,a4, spike4, spikebase, lspike1,rspike1;

static-var  nanoPt, statechg_DesiredState, statechg_StateChanging, bBuilding, terrainStatus;

// Signal definitions
#define SIG_ACTIVATE			2


Build()
{
	while (TRUE)
	{
		if( bBuilding == TRUE )
		{
			emit-sfx 1024 + 0 from beam1;
			emit-sfx 1024 + 0 from beam2;
			emit-sfx 1024 + 0 from beam3;
			sleep 80;
		}
		// hide pad if on water		
		if( terrainStatus == 2 || terrainStatus == 1 )
		{
			hide pad;
		}
		sleep 40;
	}
	return (0);
}

activatescr()
{
	sleep 20;
}

deactivatescr()
{
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

OpenYard()
{
	set YARD_OPEN to 1;
	while( !get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 1;
	}
	set BUGGER_OFF to 0;
}

CloseYard()
{
	set YARD_OPEN to 0;
	while( get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 0;
	}
	set BUGGER_OFF to 0;
}

Go()
{
	call-script activatescr();
	call-script OpenYard();
	set INBUILDSTANCE to 1;
}

Stop()
{
	set INBUILDSTANCE to 0;
	call-script CloseYard();
	call-script deactivatescr();
}

InitState()
{
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 0 )
		{
			call-script Go();
			currentstate = 0;
		}
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	terrainStatus = 0;
	nanoPt = [0];
	call-script InitState();
	start-script Build();
	start-script SmokeUnit();
}

setSFXoccupy(param1)
{
	terrainStatus = param1;
	return (0);
}

QueryNanoPiece(piecenum)
{
	if( nanoPt == [0] )
	{
		piecenum = beam1;
		nanoPt = [1];
		return (0);
	}
	if( nanoPt == [1] )
	{
		piecenum = beam2;
		nanoPt = [2];
		return (0);
	}
	if( nanoPt == [2] )
	{
		piecenum = beam3;
		nanoPt = [0];
		return (0);
	}
}


Activate()
{
	signal SIG_ACTIVATE;
	start-script RequestState(0);
}

Deactivate()
{
	signal SIG_ACTIVATE;
	set-signal-mask SIG_ACTIVATE;
	sleep 1000;
	set-signal-mask 0;
	start-script RequestState(1);
}


StartBuilding()
{
	bBuilding = TRUE;
}

StopBuilding()
{
	bBuilding = FALSE;
}

QueryBuildInfo(piecenum)
{
	piecenum = buildpt;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode platform1 type SHATTER;
		explode platform2 type SHATTER;
		explode platform3 type SHATTER;
		explode spinner1 type SHATTER;
		explode spinner2 type SHATTER;
		explode spinner3 type SHATTER;
		return (1);
	}
	explode pad type SHATTER;
	explode xr type SHATTER;
	explode xl type SHATTER;
	
	explode a1 type SHATTER;
	explode a2 type SHATTER;
	explode a3 type SHATTER;
	explode a4 type SHATTER;
	explode platform1 type SHATTER;
	explode platform2 type SHATTER;
	explode platform3 type SHATTER;
	explode spinner1 type SHATTER;
	explode spinner2 type SHATTER;
	explode spinner3 type SHATTER;
	
	return (2);
}
