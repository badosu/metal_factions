#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 180

piece  flare, base, turret,sleeve,barrel, bwheell, bwheelr, fwheell, fwheelr, mwheell, mwheelr, bcover, mcover, fcover, b2;

static-var  restore_delay;

// Signal definitions
#define SIG_AIM				2

#include "wheelspeed.h"



SpinWheels(last)
{
	last = 0;
	while ( TRUE )
	{
		
		if ( last != (wheelSpeed / 8) )
		{
			spin bwheell around x-axis speed wheelSpeed; // accelerate wheelAcceleration;
			spin bwheelr around x-axis speed wheelSpeed; //accelerate wheelAcceleration;
			spin fwheell around x-axis speed wheelSpeed; //accelerate wheelAcceleration;
			spin fwheelr around x-axis speed wheelSpeed; //accelerate wheelAcceleration;
			spin mwheell around x-axis speed wheelSpeed; //accelerate wheelAcceleration;
			spin mwheelr around x-axis speed wheelSpeed; //accelerate wheelAcceleration;

			last = wheelSpeed / 8;
		}
		sleep 100;
	}
	return (0);
}

StopWheels()
{
	stop-spin bwheell around x-axis decelerate <12.000000>;
	stop-spin bwheelr around x-axis decelerate <12.000000>;
	stop-spin fwheell around x-axis decelerate <12.000000>;
	stop-spin fwheelr around x-axis decelerate <12.000000>;
	stop-spin mwheell around x-axis decelerate <12.000000>;
	stop-spin mwheelr around x-axis decelerate <12.000000>;
	return (0);
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}

Create()
{
	hide flare;
	restore_delay = 3000;
	start-script SmokeUnit();
	start-script WheelSpeedControl();
	start-script SpinWheels();
	return (0);
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 2;
	return (0);
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	turn turret to y-axis <0.000000> speed <140.000000>;
	turn sleeve to x-axis <0.000000> speed <140.000000>;
	return (0);
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <180.000000>;
	turn sleeve to x-axis <0.000000> - pitch speed <180.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	move barrel to z-axis [-3.000000] now;
	emit-sfx 1024 from flare;
	sleep 100;
	move barrel to z-axis [0.000000] speed [5.000000];
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
	return (0);
}

QueryPrimary(piecenum)
{
	piecenum = flare;
	return (0);
}

SweetSpot(piecenum)
{
	piecenum = base;
	return (0);
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type BITMAPONLY | BITMAP1;
		explode turret type SHATTER | BITMAP4;
		explode barrel type SHATTER | BITMAP4;		
		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type BITMAPONLY | BITMAP1;
		explode turret type SHATTER | BITMAP4;
		explode barrel type SHATTER | BITMAP4;		
		explode bwheell type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode fwheelr type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode base type BITMAPONLY | BITMAP1;
		explode bwheell type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode fwheelr type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode turret type SHATTER | BITMAP4;
		explode barrel type SHATTER | BITMAP4;		
		return (0);
	}
	corpsetype = 3;
	explode base type BITMAPONLY | BITMAP1;
	explode bwheell type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode fwheelr type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode turret type SHATTER | BITMAP4;
	explode barrel type SHATTER | BITMAP4;		
	
	return (0);
}
