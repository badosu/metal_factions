#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

#define SPEEDUP_FACTOR 140

piece  base, turret,sleeve,lbarrel,lflare,rbarrel,rflare,t1,wheel1,t2,wheel2,t3,wheel3,t4,wheel4,bb;

static-var gun_1;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2

#include "wheelspeed.h"


SpinWheels(last)
{
	last = 0;
	while ( TRUE )
	{
		
		if ( last != (wheelSpeed / 8) )
		{
			spin t1 around x-axis speed wheelSpeed; // accelerate wheelAcceleration;
			spin t2 around x-axis speed wheelSpeed; //accelerate wheelAcceleration;
			spin t3 around x-axis speed wheelSpeed; //accelerate wheelAcceleration;
			spin t4 around x-axis speed wheelSpeed;
			last = wheelSpeed / 8;
		}
		sleep 100;
	}
	return (0);
}

StopWheels()
{
	stop-spin t1 around x-axis decelerate <12.000000>;
	stop-spin t2 around x-axis decelerate <12.000000>;
	stop-spin t3 around x-axis decelerate <12.000000>;
	stop-spin t4 around x-axis decelerate <12.000000>;
	return (0);
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}



HitByWeapon(Func_Var_1, Func_Var_2)
{
	turn base to z-axis Func_Var_2 speed <105.016484>;
	turn base to x-axis Func_Var_1 speed <105.016484>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to z-axis <0.000000> speed <30.005495>;
	turn base to x-axis <0.000000> speed <30.005495>;
}

Create()
{
	gun_1 = 0;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script WheelSpeedControl();
	start-script SpinWheels();
	start-script SmokeUnit();
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<200>,<200>);
	turn turret to y-axis <0.000000> speed <200.016484>;
	turn sleeve to x-axis <0.000000> speed <200.005495>;
}

AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<320>,<350>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <320.038462>;
	turn sleeve to x-axis <0.000000> - pitch speed <350.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn sleeve around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	if (gun_1 == 0) {
		emit-sfx 1024 + 0 from lflare;
		move lbarrel to z-axis [-1] now;
		gun_1 = 1;
		sleep 100;
		move lbarrel to z-axis [0] speed [2];
		return 0;
	}
	if (gun_1 == 1) {
		emit-sfx 1024 + 0 from rflare;
		move rbarrel to z-axis [-1] now;
		gun_1 = 0;
		sleep 100;
		move rbarrel to z-axis [0] speed [2];
		return 0;
	}
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	if (gun_1 == 0) {
		piecenum = lflare;
		return lflare;
	}
	if (gun_1 == 1) {
		piecenum = rflare;
		return rflare;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 35 )
	{
		explode sleeve type SHATTER;
		explode turret type SHATTER;
		explode lbarrel type SHATTER;
		return (1);
	}

	explode sleeve type SHATTER;
	explode turret type SHATTER;
	explode lbarrel type SHATTER;
	explode t1 type SHATTER;
	explode t2 type SHATTER;
	explode wheel1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode wheel2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode wheel3 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	return (2);
}
