#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 140

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>


piece  base, bcover, barrel1,flare1,barrel2,flare2, body1,body2, ldoor,rdoor,lleg1, lleg2, lleg3,
       rleg3, rleg2, rleg1, bodysect1, head, mbase, rleg22,
       rleg33, rleg11, lleg33, lleg22, lleg11;

static-var  bMoving, gun_1, mASpdFactor, isOpen;


// Signal definitions
#define SIG_AIM				2


MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = SPEEDUP_FACTOR;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		//call-script lua_cobDebug(mASpdFactor);
		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

HitByWeapon(Func_Var_1, Func_Var_2)
{
	turn base to z-axis Func_Var_2 speed <105.000000>;
	turn base to x-axis Func_Var_1 speed <105.000000>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to z-axis <0.000000> speed <30.000000>;
	turn base to x-axis <0.000000> speed <30.000000>;
}

walk()
{
	if( bMoving )
	{
		turn rleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		turn lleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		
		turn base to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;

		turn lleg1 to y-axis <20.000000> speed <100.967033> * mASpdFactor / 100;
		turn lleg1 to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;

		turn lleg2 to y-axis <-20.000000> speed <300.038462> * mASpdFactor / 100;
		turn lleg2 to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn lleg3 to y-axis <45.060440> speed <120.038462> * mASpdFactor / 100;
		turn lleg3 to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rleg1 to y-axis <40.412088> speed <300.060440> * mASpdFactor / 100;
		turn rleg1 to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		turn rleg2 to y-axis <-20.093407> speed <100.368132> * mASpdFactor / 100;
		turn rleg2 to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rleg3 to y-axis <15.137363> speed <300.681319> * mASpdFactor / 100;
		turn rleg3 to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		
		sleep 175 * 100 / mASpdFactor;
	}

	// only vertical
	if( bMoving )
	{
		turn lleg2 to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rleg1 to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rleg3 to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		
		sleep 175 * 100 / mASpdFactor;
	}
	
	
	if( bMoving )
	{
		turn base to x-axis <-2.000000> speed <20.000000> * mASpdFactor / 100;

		turn lleg1 to y-axis <-40.000000> speed <300.967033> * mASpdFactor / 100;
		turn lleg1 to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn lleg2 to y-axis <20.000000> speed <100.038462> * mASpdFactor / 100;
		turn lleg2 to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;

		turn lleg3 to y-axis <-15.060440> speed <300.038462> * mASpdFactor / 100;
		turn lleg3 to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn rleg1 to y-axis <-20.412088> speed <100.060440> * mASpdFactor / 100;
		turn rleg1 to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rleg2 to y-axis <20.093407> speed <300.368132> * mASpdFactor / 100;
		turn rleg2 to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		turn rleg3 to y-axis <-45.137363> speed <120.681319> * mASpdFactor / 100;
		turn rleg3 to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		sleep 175 * 100 / mASpdFactor;
	}
	
	// only vertical
	if( bMoving )
	{
		turn rleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		turn lleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;

		turn lleg1 to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn lleg3 to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rleg2 to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
	
		
		sleep 175 * 100 / mASpdFactor;
	}
	
}


Stop()
{
	turn base to z-axis <0.000000> speed <7.000000>;
	turn base to x-axis <0.000000> speed <7.000000>;
	turn lleg1 to y-axis <-25.000000> speed <237.000000>;
	turn lleg1 to z-axis <0.000000> speed <237.000000>;
	turn lleg2 to y-axis <0.000000> speed <237.000000>;
	turn lleg2 to z-axis <0.000000> speed <237.000000>;
	turn lleg3 to y-axis <0.000000> speed <237.000000>;
	turn lleg3 to z-axis <0.000000> speed <237.000000>;
	turn rleg3 to y-axis <0.000000> speed <237.000000>;
	turn rleg3 to z-axis <0.000000> speed <237.000000>;
	turn rleg2 to y-axis <0.000000> speed <237.000000>;
	turn rleg2 to z-axis <0.000000> speed <237.000000>;
	turn rleg1 to y-axis <25.000000> speed <237.000000>;
	turn rleg1 to z-axis <0.000000> speed <237.000000>;

}

MotionControl(moving)
{
	while( TRUE )
	{
		moving = bMoving;
		if( moving )
		{
			call-script walk();
		}
		if( !moving )
		{
			call-script Stop();
			sleep 100;
		}
	}
}

Create()
{
	bMoving = FALSE;
	isOpen = FALSE;
	gun_1 = [0];
	turn lleg3 to y-axis <20.000000> speed <300.000000>;
	turn rleg3 to y-axis <-20.000000> speed <300.000000>;
	turn rleg1 to y-axis <30.000000> speed <300.000000>;
	turn lleg1 to y-axis <-30.000000> speed <300.000000>;
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}


Open()
{
	if (!isOpen)
	{
		set ARMORED to FALSE;
		turn rdoor to z-axis <-90.000000> speed <290.000000>;
		turn ldoor to z-axis <90.000000> speed <290.000000>;
		wait-for-turn ldoor around z-axis;
		isOpen = TRUE;
	}
}

Close()
{
	if (isOpen)
	{
		isOpen = FALSE;
		turn rdoor to z-axis <0.000000> speed <190.000000>;
		turn ldoor to z-axis <0.000000> speed <190.000000>;
		wait-for-turn ldoor around z-axis;
		set ARMORED to TRUE;
		sleep 200;
	}
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script Close();
	return (0);
}
SweetSpot(piecenum)
{
	piecenum = base;
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	if (!isOpen)
	{
		call-script Open();
	}
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	if (gun_1 == [0])
	{
		emit-sfx 1024+0 from flare1;
		sleep 100;
		gun_1 = [1];
		return (0);
	}
	if (gun_1 == [1])
	{
		emit-sfx 1024+0 from flare2;
		sleep 100;
		gun_1 = [0];
		return (0);
	}
}

AimFromPrimary(piecenum)
{
	piecenum = mbase;
}

QueryPrimary(piecenum)
{
	if (gun_1 == [0])
	{
		piecenum = flare1;
		return (0);
	}
	if (gun_1 == [1])
	{
		piecenum = flare2;
		return (0);
	}
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode bodysect1 type SHATTER;
		explode barrel1 type SHATTER;
		explode lleg2 type SHATTER;
		explode rleg3 type SHATTER;
		return (1);
	}
	explode head type SHATTER | SMOKE | EXPLODE_ON_HIT | BITMAP3;
	explode bodysect1 type SHATTER;
	explode lleg1 type SHATTER;
	explode lleg2 type SHATTER;
	explode lleg3 type SHATTER;
	explode rleg3 type SHATTER;
	explode rleg2 type SHATTER;
	explode rleg1 type SHATTER;
	explode bcover type SHATTER;
	return (2);
}
