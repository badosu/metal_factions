#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, pad, beam1, beam2, platform1, platform2,
       nano2, nano1, a1, a2,x1,x2,t1,t2, rails;

static-var  nanoPt, bBuilding, statechg_DesiredState, statechg_StateChanging;

// Signal definitions
#define SIG_ACTIVATE			2



Build()
{
	while (TRUE)
	{
		if( bBuilding == TRUE )
		{
			emit-sfx 1024 + 0 from beam1;
			emit-sfx 1024 + 0 from beam2;
			sleep 80;
		}
		sleep 40;
	}
	return (0);
}


activatescr()
{
	move a1 to x-axis [19.000000] speed [15.000000];
	move a2 to x-axis [-19.000000] speed [15.000000];
	wait-for-move a1 along x-axis;
	wait-for-move a2 along x-axis;

	move platform1 to y-axis [6.000000] speed [12.000000];
	move platform2 to y-axis [6.000000] speed [12.000000];
	move t1 to z-axis [10.000000] speed [12.000000];
	move t2 to z-axis [10.000000] speed [12.000000];
	
	wait-for-move platform1 along y-axis;
	wait-for-move platform2 along y-axis;
	wait-for-move t1 along z-axis;
	wait-for-move t2 along z-axis;

	move nano1 to x-axis [-3.000000] speed [12.000000];
	move nano2 to x-axis [3.000000] speed [12.000000];
	wait-for-move nano1 along x-axis;
	wait-for-move nano2 along x-axis;
	
	sleep 20;
}

deactivatescr()
{
	move nano1 to x-axis [0.000000] speed [10.000000];
	move nano2 to x-axis [0.000000] speed [10.000000];
	wait-for-move nano1 along x-axis;
	wait-for-move nano2 along x-axis;

	move platform1 to y-axis [0.000000] speed [10.000000];
	move platform2 to y-axis [0.000000] speed [10.000000];
	move t1 to z-axis [0.000000] speed [10.000000];
	move t2 to z-axis [0.000000] speed [10.000000];
	
	wait-for-move platform1 along y-axis;
	wait-for-move platform2 along y-axis;
	wait-for-move t1 along z-axis;
	wait-for-move t2 along z-axis;

	move a1 to x-axis [0.000000] speed [10.000000];
	move a2 to x-axis [0.000000] speed [10.000000];
	wait-for-move a1 along x-axis;
	wait-for-move a2 along x-axis;
	
	sleep 20;
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

OpenYard()
{
	set YARD_OPEN to 1;
	while( !get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 1;
	}
	set BUGGER_OFF to 0;
}

CloseYard()
{
	set YARD_OPEN to 0;
	while( get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 0;
	}
	set BUGGER_OFF to 0;
}

Go()
{
	call-script activatescr();
	call-script OpenYard();
	set INBUILDSTANCE to 1;
}

Stop()
{
	set INBUILDSTANCE to 0;
	call-script CloseYard();
	call-script deactivatescr();
}

InitState()
{
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 0 )
		{
			call-script Go();
			currentstate = 0;
		}
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	dont-shade beam1;
	dont-cache base;
	dont-shade base;
	dont-shade beam2;
	dont-shade platform1;
	dont-shade platform2;

	dont-cache nano1;
	dont-cache nano2;

	dont-shade nano1;
	dont-shade nano2;

	dont-shade pad;
	dont-cache pad;

	bBuilding = FALSE;
	nanoPt = 111;
	call-script InitState();
	start-script SmokeUnit();
	start-script Build();
}

QueryNanoPiece(piecenum)
{
	if( nanoPt == 111 )
	{
		piecenum = beam1;
		nanoPt = 222;
		return (0);
	}
	if( nanoPt == 222 )
	{
		piecenum = beam2;
		nanoPt = 111;
		return (0);
	}

}

Activate()
{
	signal SIG_ACTIVATE;
	start-script RequestState(0);
}

Deactivate()
{
	signal SIG_ACTIVATE;
	set-signal-mask SIG_ACTIVATE;
	sleep 5000;
	set-signal-mask 0;
	start-script RequestState(1);
}

StartBuilding()
{
	bBuilding = TRUE;
}

StopBuilding()
{
	bBuilding = FALSE;
}

QueryBuildInfo(piecenum)
{
	piecenum = pad;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode nano1 type SHATTER;
		explode nano2 type SHATTER;
		explode platform1 type SHATTER;
		explode platform2 type SHATTER;
		return (1);
	}
	explode nano1 type SHATTER;
	explode nano2 type SHATTER;
	explode platform1 type SHATTER;
	explode platform2 type SHATTER;
	explode a1 type SHATTER;
	explode a2 type SHATTER;
	explode x1 type SHATTER;
	explode x2 type SHATTER;

	return (2);
}
